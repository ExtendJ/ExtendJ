plugins {
  id 'java'
  id 'application'
  id 'org.jastadd'
}

sourceCompatibility = targetCompatibility = '1.6'
compileJava.options.encoding = 'UTF-8'

dependencies {
  jastadd2 "org.jastadd:jastadd:2.3.5"
}

application {
  mainClass = 'org.extendj.JavaCompiler'
}

jar {
  archiveFileName = 'extendj.jar'
  manifest.attributes 'Main-Class': application.mainClass
  destinationDirectory = projectDir
}

jastadd {
  configureModuleBuild()

  modules rootProject.file('jastadd_modules')

  module = 'java5 backend'

  astPackage = 'org.extendj.ast'
  genDir = 'src/gen'
  buildInfoDir = 'src/gen-res'
  parser.name = 'JavaParser'
  parser.genDir = 'src/gen/org/extendj/parser'
  scanner.genDir = 'src/gen/org/extendj/scanner'

  if (rootProject.hasProperty('extraJastAddOptions')) {
    extraJastAddOptions += rootProject.extraJastAddOptions.split(',') as List
    print("options: ${extraJastAddOptions}")
  }
}

sourceSets {
  main.java {
    srcDir rootProject.file('src/backend-main')
    srcDir rootProject.file('src/frontend-main')
    srcDir 'src/gen'
  }
  main.resources {
    srcDir 'src/gen-res' // JastAdd buildInfo directory.
  }
}
